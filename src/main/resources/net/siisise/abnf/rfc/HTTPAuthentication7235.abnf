auth-scheme = token
auth-param = token BWS "=" BWS ( token / quoted-string )
token68 = 1*( ALPHA / DIGIT /
        "-" / "." / "_" / "~" / "+" / "/" ) *"="
challenge   = auth-scheme [ 1*SP ( token68 / #auth-param ) ]
credentials = auth-scheme [ 1*SP ( token68 / #auth-param ) ]
WWW-Authenticate = 1#challenge
Authorization = credentials
Proxy-Authenticate = 1#challenge
Proxy-Authorization = credentials

BWS = OWS
OWS = *( SP / HTAB )
quoted-string = <quoted-string, see [RFC7230], Section 3.2.6>
token = <token, see [RFC7230], Section 3.2.6>
